<html>
  <head>
    <title>&nbsp;&nbsp;Roobles Production (Casuals Edition) v0.0.2</title>
    <HTA:APPLICATION
      APPLICATIONNAME="Roobles Production (Casuals Edition)"
      ID="RooblesProdStal"
      VERSION="1.0"
      ICON="Roobles.ico"
      BORDER="DIALOG"
      MAXIMIZEBUTTON="NO"
    />
    <style>

body {
  cursor: progress;
  color: #3F225A;
  font-family: Arial, sans-serif;
  background-image: url("rpsbg.png");
  padding: 0px;
  margin: 0px;
  height: 100%
}

#content {
  width: 900px;
  background: #fff0f8;
  border-style: solid;
  border-color: #7d6080;
  border-width: 0px 3px 0px 2px;
  margin-left: 40px;
  padding: 50px 10px 0px 10px;
  height: 100%
}

h2 {
  width: 100%;
  color: #463092;
  font-size: 1.8em;
  font-weight: 700;
  padding-bottom: 5px;
  border-color: #463092;
  border-style: solid;
  border-width: 0px 0px 2px 0px
}

form {
  padding-left: 10px;
  margin-bottom: 50px;
  opacity: 0.5
}

label, .submitButton {
  font-size: x-small;
  margin-top: 20px;
  display: block
}

span .formSection {
  display: block;
  width: 100%
}

span .green {
  color: #A7BA59 
}

span .yellow {
  color: #F0D890
}

span .lightyellow {
  color: #EDE0B9
}

span .red {
  color: #AE2F27
}

span .blue {
  color: #7F94B0
}

span .purple {
  color: #85B394
}

.inputText {
  width: 860px;
  height: 40px;
  padding: 5px 15px;
  border-style: solid;
  border-color: #7d6080;
  border-width: 2px 1px 1px 2px;
  display: inline-block
}

.withButton {
  width: 750px;
  margin-right: 10px
}

.inlineButton {
  width: 100px;
  height: 40px;
  margin: 0px;
  margin-left: -10px;
  padding: 0px
}

.buttonRow {
  display: block;
  margin-top: 20px
}

#stdout {
  font-family: lucida console, monospace;
  color: #F0F0D8;
  font-size: 10pt;
  font-weight: 300;
  line-height: 1.15;
  background-image: url("vtbg.png");
  background-position: 50px 110px;
  background-attachment: fixed;

  padding-left: 5px;
  margin-right: 10px;
  border-style: solid;
  border-color: #7d6080;
  border-width: 2px 1px 1px 2px;
  overflow: scroll;
  white-space: nowrap;
  height: 350px;
  width: 100%
}

.submitButton, .inlineButton, .buttonRow {
  font-weight: 500;
}
    </style>
    <script language="VBScript">

Dim ratz, ratzSet, ratzChance
Dim matchRegex, tournRegex, coreRegexVal, quoteRegex
Dim winWidth, winHeight, inputForm, outputForm, scriptDir, logoScript, statScript, stdOutFile, ratzFile, statsFile
Dim defaultDir, gitBash, logoSaveScriptName, statGetScriptName, stdOutFileName, ratzFileName
Dim faceitIdType
Const faceit_type_unknown = 0, faceit_type_match = 1, faceit_type_tourn = 2
Const ForReading = 1, ForAppending = 8

winWidth = 1550
winHeight = 1000

defaultDir = "C:\tmp\logo"
gitBash = "C:\Program Files\Git\bin\bash.exe"
logoSaveScriptName = "save-team-logo.sh"
statGetScriptName = "get-match-stats.sh"
stdOutFileName = "stdout.txt"
ratzFileName = "web.ratz"

coreRegexVal = "[a-f0-9]{8}(-[a-f0-9]{4}){3}-[a-f0-9]{12}"

set matchRegex = New RegExp
With matchRegex 
  .Pattern = "^1-" + coreRegexVal + "$"
  .IgnoreCase = True
End With

set tournRegex = New RegExp
With tournRegex 
  .Pattern = "^" + coreRegexVal + "$"
  .IgnoreCase = True
End With

set quoteRegex = new RegExp
With quoteRegex
  .Pattern = "(""[^""]+"")"
  .Global = true
End With

set argRegex = new RegExp
With argRegex
  .Pattern = "(--?[a-zi0-9]+) "
  .IgnoreCase = true
  .Global = true
End With

faceitIdType = faceit_type_unknown

scriptDir = CreateObject("Scripting.FileSystemObject").GetParentFolderName(document.location.pathname)
logoScript = scriptDir + "\" + logoSaveScriptName
statScript = scriptDir + "\" + statGetScriptName
stdOutFile = scriptDir + "\" + stdOutFileName
ratzFile = scriptDir + "\" + ratzFileName
statsFile = scriptDir + "\stats\stats.js"
ratz = Array(0)
ratzSet = false
ratzChance = 3

Sub Window_OnLoad
  window.resizeto winWidth,winHeight

  set inputForm = document.forms.InputForm
  set outputForm = document.forms.OutputForm

  inputForm.logoDir.value = defaultDir

  ClearOutput
  EnableInput
End Sub

Sub BrowseForLogoDir
  Dim browseResult
  browseResult = PickFolder(Nothing, "Set a location for saving team logos:")

  If browseResult <> "" Then
    inputForm.logoDir.value = browseResult
  End If
End Sub

Sub AppendLineToOutput(outputData)
  Const ForReading = 1
  Const ForAppending = 8

  Dim fso, fileHandle
  Set fso = CreateObject("Scripting.FileSystemObject")
  Set fileHandle = fso.OpenTextFile(stdOutFile, ForAppending, True)

  fileHandle.WriteLine outputData + "<br/>"

  fileHandle.Close
  Set fileHandle = Nothing
  Set fso = Nothing

  UpdateOutput
End Sub

Sub UpdateOutput
  Dim fso, file

  Set fso = CreateObject("Scripting.FileSystemObject")
  Set file = fso.OpenTextFile(stdOutFile, 1)

  document.getElementById("stdout").innerHtml = file.ReadAll

  file.Close
  Set file = Nothing
  Set fso = Nothing
End Sub

Sub ClearOutput
  Dim fso

  Set fso = CreateObject("Scripting.FileSystemObject")

  If fso.FileExists(stdOutFile) Then
    fso.DeleteFile(stdOutFile)
  End If

  document.getElementById("stdout").innerHtml = ""
  Set fso = Nothing
End Sub

Sub SaveLogos 
  Dim scriptCmd
  Dim faceitId, logoDir, faceitType

  faceitId = inputForm.faceitId.value
  logoDir = inputForm.logoDir.value

  If faceitId = "" Then
    MsgBox "Must Provide a FaceIt ID.", vbCritical, "Error"
    Exit Sub
  End If

  Select case faceitIdType
    case faceit_type_match
      faceitType = "match"

    case faceit_type_tourn
      faceitType = "tournament"

    case else
      MsgBox "Could not determine FaceIT Id Type.", vbCritital, "Error"
      Exit Sub
  End Select

  scriptCmd = """" + logoScript + """ -I """ + faceitId + """ -l """ + logoDir + """ -wo """ + stdOutFile + """ -T " + faceitType 
  RunShellScript scriptCmd
End Sub

Sub GetStats
  Dim scriptCmd, faceitId

  faceitId = inputForm.faceitId.value

  If faceitId = "" Then
    MsgBox "Must Provide a FaceIt ID.", vbCritical, "Error"
    Exit Sub
  End If

  scriptCmd = """" + statScript + """ -I """ + faceitID + """ -wo """ + stdOutFile + """ -W """ + statsFile + """"
  RunShellScript scriptCmd
End Sub

Sub RunShellScript(scriptCmd)
  Dim Shell, rtrnCode, ctlCmd
  Dim intervalHandle

  ctlCmd = """" + gitBash + """ --login -c '" + scriptCmd + "'"

  AppendLineToOutput "<br />" + InsertSyntaxHighlighting(ctlCmd) + "<br />"

  DisableInput
  Set Shell=CreateObject("wscript.shell")
    intervalHandle = window.setInterval("UpdateOutput", 1000)
    Shell.run ctlCmd, 0, True
    window.clearInterval intervalHandle 
  Set Shell=Nothing

  InjectRat
  EnableInput
End Sub

Function PickFolder(rootFolder, promptDescription)
    Dim shell, oFldr, options
    Set shell = CreateObject("Shell.Application")

    options = &H0001 + &H0004 + &H0010 + &H0020 + &H0040 + &H0100
    Set oFldr = shell.BrowseForFolder(0, promptDescription, options, rootFolder)
    If (Not oFldr Is Nothing) Then
        PickFolder = oFldr.Self.Path
    Else
        PickFolder = ""
    End If
    Set shell = Nothing
    Set oFldr = Nothing
End Function

Function ValidateDownloadLogos
  Dim faceitId, fieldLabel, baseLabel
  Dim labelSuffix, isValid

  faceitId = inputForm.faceitId.value
  set fieldLabel = document.getElementById("faceitIdLabel")
  baseLabel = "FaceIT Id"

  If matchRegex.Test(faceitId) Then
    isValid = true 
    labelSuffix = " (Match):"
    faceitIdType = faceit_type_match

  ElseIf tournRegex.Test(faceitId) Then
    isValid = true
    labelSuffix = " (Tournament):"
    faceitIdType = faceit_type_tourn

  Else
    isValid = false
    labelSuffix = ":"
    faceitIdType = faceit_type_unknown 
  End If

  fieldLabel.innerHtml = baseLabel + labelSuffix
  ValidateDownloadLogos = isValid
End Function

Function ValidateGetMatchStats
  Dim faceitId
  faceitId = inputForm.faceitId.value

  ValidateGetMatchStats = matchRegex.Test(faceitId)
End Function

Sub EnableInputElement(element)
  element.disabled = false
  element.style.cursor = "auto"
End Sub

Sub DisableInputElement(element)
  element.disabled = true
  element.style.cursor = "progress"
End Sub

Sub EnableInput
  document.body.style.cursor = "auto"

  EnableInputElement inputForm.faceitId
  EnableInputElement inputForm.logoDir
  EnableInputElement inputForm.browseDir
  EnableInputElement inputForm.saveLogoButton
  EnableInputElement inputForm.getStatsButton
  EnableInputElement outputForm.clearOutputBtn

  EnableSubmit
End Sub

Sub EnableSubmit
  inputForm.saveLogoButton.disabled = Not ValidateDownloadLogos
  inputForm.getStatsButton.disabled = Not ValidateGetMatchStats
End Sub

Sub DisableInput
  document.body.style.cursor = "progress"

  DisableInputElement inputForm.faceitId
  DisableInputElement inputForm.logoDir
  DisableInputElement inputForm.browseDir
  DisableInputElement inputForm.saveLogoButton
  DisableInputElement inputForm.getStatsButton
  DisableInputElement outputForm.clearOutputBtn
End Sub

Function GetRatArray
  If ratzSet Then
    GetRatArray = ratz
    Exit Function
  End If

  Dim fso, file
  Set fso = CreateObject("Scripting.FileSystemObject")
  Set file = fso.OpenTextFile(ratzFile, ForReading)

  Do While file.AtEndOfStream <> True
    Redim Preserve ratz(UBound(ratz) + 1)
    ratz(UBound(ratz)) = file.ReadLine
  Loop

  file.Close
  Set file = Nothing
  Set fso = Nothing

  GetRatArray = ratz
End Function

Sub InjectRat

  Dim ratties, ratCount

  ratties = GetRatArray()
  ratCount = UBound(ratties)
  randomize
  AppendLineToOutput "<br/>" + ratties(int(rnd*ratCount)+1)
End Sub

Sub TryInjectRat(rChance)

  randomize
  If (int(rnd * rChance)) > 0 Then
    Exit Sub
  End If

  InjectRat
End Sub

Function InsertSyntaxHighlighting(outputTxt)
  
  InsertSyntaxHighlighting = argRegex.Replace(quoteRegex.Replace(outputTxt, "<span class=""blue"">$1</span>"), "<span class=""lightyellow"">$1</span> ")
End Function

    </script>
  </head>

  <body scroll="no">
    <div id="Content">
      <h2> Save Team Logos </h3>
      <form id="InputForm">
        <div>
          <label id="faceitIdLabel" for="faceitId">FaceIt ID:</label>
          <span class="formSection">
            <input class="inputText" type="text" name="faceitId" id="faceitId" onchange="EnableSubmit" required />
          </span>
        </div>
        <div>
          <label for="logoDir">Logo Directory:</label>
          <span class="formSection">
            <input class="inputText withButton" type="text" name="logoDir" id="logoDir" />
            <input type="button" class="inlineButton" name="browseDir" id="browseDir" value="Browse"  onclick="BrowseForLogoDir" />
          </span>
        </div>
        <div>
          <span class="buttonRow">
            <input id="saveLogoButton" type="button" value="Download Logos" onclick="SaveLogos"/>
            <input id="getStatsButton" type="button" value="Get Stats" onclick="GetStats"/>
          </span>
        </div>
      </form>
      <h2> Output </h3>
      <form id="OutputForm">
        <span class="formSection">
          <div name="stdout" id="stdout">
          </div>
        </span>
        <span class="buttonRow">
          <input type="button" name="clearOutputBtn" id="clearOutputBtn" value="Clear" onclick="ClearOutput" />
        </span>
      </form>
    </div>
  </body>

</html>
